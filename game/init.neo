const Vec2 = require("scripts/math/Vec2.neo");

let a: Vec2 = new Vec2 { x = 0.1; y = 0.2; };
let b: Vec2 = new Vec2 { x = -0.1; y = -0.2; };
let c: Vec2 = a + b; // { 0., 0. }
let d: Vec2 = a - b; // { 0.2, 0.4 }
let e: Vec2 = d * 2; // { 0.4, 0.8 }
print(c.x, ", ", c.y, "\n");
print(d.x, ", ", d.y, "\n");
print(e.x, ", ", e.y, "\n");
/* The best
 * triangle known to man kind. */
fn sierpinski(n: Int) {
  for (let y: Int = n - 1; y >= 0; y--) {
    for (let i = 0; i < y; i++)
      print(" ");
    for (let x: Int = 0; x + y < n; x++)
      if (x & y) print("  "); else print("* ")
    print("\n");
  }
}

sierpinski(16);
sierpinski(32);
sierpinski(64);

fn hofstadter_conway(m: Int) {
  let k1: Int = 2;
  let lg2: Int = 1;
  let amax: Float = 0.0;
  let list: Array = [ -50000, 1, 1 ].resize(0x100000 + 1);
  let v: Int = list[2];
  for (let n: Int = 3; n <= m; n++) {
    list[n] = list[v] + list[n-v];
    v = list[n];
    if (amax < v*1.0/n) amax = v*1.0/n;
    if (0 == (k1&n)) {
      print("Maximum between 2^", lg2, " and 2^", lg2+1, " was ", amax, "\n");
      amax = 0.0;
      lg2++;
    }
    k1 = n;
  }
}

//hofstadter_conway(0x100000);

fn thieles_interpolation() {
  const N: Int = 32;
  const N2: Int = (N * (N - 1) / 2);
  const STEP: Float = 0.05;
  let xval: Array = [].resize(N);
  let tsin: Array = [].resize(N);
  let tcos: Array = [].resize(N);
  let ttan: Array = [].resize(N);
  let rsin: Array = [].resize(N2);
  let rcos: Array = [].resize(N2);
  let rtan: Array = [].resize(N2);
  fn rho(x: Array, y: Array, r: Array, i: Int, n: Int) {
      if (n < 0) return 0;
      if (n == 0) return y[i];
      let idx = (N - 1 - n) * (N - n) / 2 + i;
      if (r[idx] != r[idx])
          r[idx] = (x[i] - x[i + n]) / (rho(x, y, r, i, n - 1) - rho(x, y, r, i + 1, n - 1)) + rho(x, y, r, i + 1, n - 2);
      return r[idx];
  }
  fn thiele(x: Array, y: Array, r: Array, xin: Float, n: Int) {
    if (n > N - 1) return 1;
    return rho(x, y, r, 0, n) - rho(x, y, r, 0, n - 2)
      + (xin - x[n]) / thiele(x, y, r, xin, n + 1);
  }
  fn isin(x: Float) { return thiele(tsin, xval, rsin, x, 0); }
  fn icos(x: Float) { return thiele(tcos, xval, rcos, x, 0); }
  fn itan(x: Float) { return thiele(ttan, xval, rtan, x, 0); }
  for (let i: Int = 0; i < N; i++) {
    xval[i] = i * STEP;
    tsin[i] = Math.sin(xval[i]);
    tcos[i] = Math.cos(xval[i]);
    ttan[i] = Math.tan(xval[i]); // optionally approximate with division: tsin[i] / tcos[i];
  }
  for (let i: Int = 0; i < N2; i++) {
    rsin[i] = 0.0/0.0;
    rcos[i] = 0.0/0.0;
    rtan[i] = 0.0/0.0;
  }
  print(6. * isin(.5), "\n");
  print(3. * icos(.5), "\n");
  print(4. * itan(1.), "\n");
}

thieles_interpolation();

fn ackermann(m: Int, n: Int) {
  if (m == 0) return n + 1;
  if (n == 0) return ackermann(m - 1, 1);
  return ackermann(m - 1, ackermann(m, n - 1));
}

for (let m: Int = 0; m <= 5; m++)
  for (let n: Int = 0; n < 6 - m; n++)
    print("A(", m, ", ", n, ") = ", ackermann(m, n), "\n");
